import str
import math
import error

# Comments are defined like this
# Each file is a 'module'
# A module is a series of declarations
# Either class, function or variable

double a 
# Only floating-point type available since VM stack slots are
# 64 bits anyways, so floats will be a waste of space.
# The only place where there could be memory savings are heap-allocated arrays,
# but those aren't really worth the extra complexity.
# Thus all variable types will be made to utilize the full 64 bits.

int x # 64 bit signed int

uint y # 64 bit unsigned int

string s # Heap allocated strings

bool b # Boolean

void main():
	
	# Here we have a standard, C like syntax
	b = !(3 > 6)
	
	print('How old are you?\n')

    # Standard Carbon has no input() function yet, so we can pretend here :)
	s = '24'
	
	# Errors are heap-allocated objects.
	# They are the standard way in which functions tell you about errors.
	# Any function that can error out will take an error argument.
	# Usually, this is the last argument.
	# If a function can throw an error, it should be checked first
	# before doing anything with its return value.
	# If a function errorrs out, its return value should not be used.

	@Error e = @Error() # Since we discourage null pointers, 
					    # most objects are automatically initialized.
	# ints should be used only if negative values are explicitly required
	# In all other cases use uint
	uint age = str2u(s, 
						e)  # function from the 'string' module
	# Statements can be split across lines, as long as the last token on each line
	# except the last cannot end a statement. Automatic semicolon insertion works like in Go.
	
	b = 3 > 6; b = !b # Explicit semicolons are still supported.

	# Errors have 2 properties
	# 1) Code
	if e.code == 0:
		# Code zero means that everything went alright
		# Everything else is an error, each code representing one possible error
		# Now that we know nothing went wrong, we can safely utilize the return value
		if age >= 18:
			print('You can drive.\n')
		else:
			print('You cannot drive.\n')
		end

	# The user entered a random string
	else:
		print('Please input a valid number\n')
	end
		
	# 2) message
	if e.msg:
		print(e.msg) # String describing the problem
	end

	# While loops function as expected
	#
	uint i = 0
	while i < 10:
		print(i += 1)
	end

	# Arrays are heap allocated
	array<int> arr = [ 3, -6, 
							9, 5 ]
	# They can be initialized like so
	array<string> names = [<
							string, # Member type
							8, 		# Initial size
							'John'>] 	# Default member value 

	# Their size can grow at runtime
	arr.append(4)

	# We can also access their length
	print(arr.length)

	# Members can be accessed like so
	print(arr[2]) # 9
	
	# Python-like for(each) loops
	for member in names:
		print(member + '\n')
	end

	# Array initializers are standalone expressions, which means	
	# They can be used on their own and passed around like values
	for n in [<bool, 12, false>]:
		print(n)
	end
	# Prints 'false' 12 times

	# Instances can be created like this
	@Vector a = @Vector(3,4)

	# Member access functions as you would expect
	print(a.Magnitude())

end


class @Vector:
		
	double a
	double b
	
	void Vector(double a, double b):
		self.a = a
		self.b = b
	end
	
	double Magnitude():
		return sqrt(self.a * self. a + self.b * self.b)
	end
	
end
